plugins {
    id 'java'
    id 'application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 主にStringUtils を利用
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    // フォルダ削除
    implementation 'commons-io:commons-io:2.18.0'
    // yml 読み込み
    implementation("org.yaml:snakeyaml:2.2")
    // jdbc
    runtimeOnly 'org.postgresql:postgresql:42.7.7'
    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

application {
    mainClass = 'jp.green_code.spring_jdbc_codegen.Main'
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

tasks.register('fatJar', Jar) {
    archiveBaseName.set('spring_jdbc_codegen')
    archiveVersion.set('1.1')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

test {
    useJUnitPlatform()
    systemProperty "user.dir", projectDir
}